(ns vllm.tokenizer-test
  (:require [clojure.test :refer [deftest is]] [vllm.tokenizer :as tokenizer]))

(def metadata
  {"tokenizer.ggml.model" "llama"
   "tokenizer.ggml.tokens" ["<unk>" "<s>" "</s>" "▁hello" "▁world" "!" "▁" "world"]
   "tokenizer.ggml.scores" [-10 -10 -10 2 2 1 -2 0]
   "tokenizer.ggml.token_type" [2 3 3 1 1 1 1 1]
   "tokenizer.ggml.bos_token_id" 1
   "tokenizer.ggml.eos_token_id" 2
   "tokenizer.ggml.unknown_token_id" 0})

(deftest sentencepiece-viterbi-roundtrip
  (let [t (tokenizer/from-metadata metadata)
        ids (tokenizer/encode t "hello world!" {:add-eos? true})]
    (is (= [1 3 4 5 2] ids))
    (is (= "hello world!" (tokenizer/decode t ids)))))

(deftest renders-common-checkpoint-chat-protocols
  (let [messages [{"role" "system" "content" "Be brief."}
                  {"role" "user" "content" "Hi"}]
        build #(tokenizer/from-metadata
                (assoc metadata "tokenizer.ggml.tokens" %
                       "tokenizer.ggml.scores" (repeat (count %) 0)
                       "tokenizer.ggml.token_type" (repeat (count %) 1)
                       "tokenizer.ggml.bos_token_id" nil))]
    (is (= "<|start_header_id|>system<|end_header_id|>\n\nBe brief.<|eot_id|><|start_header_id|>user<|end_header_id|>\n\nHi<|eot_id|><|start_header_id|>assistant<|end_header_id|>\n\n"
           (tokenizer/render-chat (build ["<|start_header_id|>" "<|eot_id|>"])
                                  messages)))
    (is (= "<|im_start|>system\nBe brief.<|im_end|>\n<|im_start|>user\nHi<|im_end|>\n<|im_start|>assistant\n"
           (tokenizer/render-chat (build ["<|im_start|>" "<|im_end|>"]) messages)))
    (is (= "<start_of_turn>system\nBe brief.<end_of_turn>\n<start_of_turn>user\nHi<end_of_turn>\n<start_of_turn>model\n"
           (tokenizer/render-chat (build ["<start_of_turn>" "<end_of_turn>"]) messages)))
    (is (= "<s>[INST] <<SYS>>\nBe brief.\n<</SYS>>\n\nHi [/INST]"
           (tokenizer/render-chat (build ["[INST]" "<s>" "</s>"]) messages)))))
